buildscript {
    ext {
        kotlinVersion = '1.3.21'
        archNavigationVersion = '1.0.0-rc02'
    }
    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.android.tools.build.jetifier:jetifier-processor:1.0.0-beta04'
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${kotlinVersion}"
    }
}

ext {
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'
    minSdkVersion = 25
    targetSdkVersion = 28

    androidxCollectionVersion = '1.0.0'
    androidxCoreVersion = '1.0.1'
    androidxFragmentVersion = '1.0.0'
    androidxAppcompatVersion = '1.0.2'
    androidxAnnotationVersion = '1.0.1'
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'http://dl.bintray.com/kotlin/kotlinx' }
    }

    configurations.all {
        resolutionStrategy {
            force "androidx.annotation:annotation:${androidxAnnotationVersion}"
            force "androidx.appcompat:appcompat:${androidxAppcompatVersion}"
            force "androidx.core:core:${androidxCoreVersion}"
            force "androidx.core:core-ktx:${androidxCoreVersion}"
            force "androidx.fragment:fragment-ktx:${androidxFragmentVersion}"

            dependencySubstitution {
                substitute module("org.jetbrains.kotlin:kotlin-stdlib-jre7") with module("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}")
            }
        }
    }

    plugins.withId('kotlin-android') {
        project.android.sourceSets*.java.each {
            it.srcDirs = it.srcDirs.collect {
                it.name == 'java' ? new File(it.parentFile, 'kotlin') : it
            }
        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}"
            if (!project.plugins.hasPlugin('com.android.test')) {
                testImplementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
                testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlinVersion}"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
